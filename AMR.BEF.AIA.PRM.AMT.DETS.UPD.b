*-----------------------------------------------------------------------------------------------------------------
* Description           : Create new fields "Occupation" and "Total Monthly Individual Revenue" in LO (SC/IC)
*
* Developed By          : Seit Lyheang
*
* Development Reference : CHG0033351
*
* Attached To           : EM.LO.APPLICATION,REVIEW.APPROVAL
*
* Attached As           : Before Auth routine
*-----------------------------------------------------------------------------------------------------------------

******************************************
SUBROUTINE AMR.BEF.AIA.PRM.AMT.DETS.UPD
******************************************

    $INSERT I_COMMON
    $INSERT I_EQUATE
    $INSERT I_F.CUSTOMER
    $INSERT I_F.AMR.AIA.LO.PRM.AMT
    $INSERT I_F.EM.LO.APPLICATION
	$INSERT I_F.SECTOR
	$INSERT I_F.INDUSTRY
	$INSERT I_F.EM.INC.EXP
	$INSERT I_F.MCB.GROUP
	$INSERT I_F.GIC.SALARY.RANGE
    
    GOSUB INIT
    GOSUB PROCESS
	GOSUB CHECKLOCN
	
******************************************    
RETURN
******************************************

***************
INIT:
***************

    FN.CUSTOMER = 'F.CUSTOMER'
    F.CUSTOMER = ''
    CALL OPF(FN.CUSTOMER,F.CUSTOMER)

    FN.AMR.AIA.LO.PRM.AMT = 'F.AMR.AIA.LO.PRM.AMT'
    F.AMR.AIA.LO.PRM.AMT = ''
    CALL OPF(FN.AMR.AIA.LO.PRM.AMT,F.AMR.AIA.LO.PRM.AMT)
	
	FN.EM.LO.APPLICATION = 'F.EM.LO.APPLICATION'
	F.EM.LO.APPLICATION = ''
	CALL OPF(FN.EM.LO.APPLICATION,F.EM.LO.APPLICATION)
	
	FN.SECTOR = 'F.SECTOR'
	F.SECTOR = ''
	CALL OPF(FN.SECTOR,F.SECTOR)
	
	FN.INDUSTRY = 'F.INDUSTRY'
	F.INDUSTRY = ''
	CALL OPF(FN.INDUSTRY,F.INDUSTRY)
	
	FN.EM.INC.EXP = 'F.EM.INC.EXP'
	F.EM.INC.EXP = ''
	CALL OPF(FN.EM.INC.EXP,F.EM.INC.EXP)
	
	FN.MCB.GROUP = 'F.MCB.GROUP'
	F.MCB.GROUP = ''
	CALL OPF(FN.MCB.GROUP,F.MCB.GROUP)
	
	FN.GIC.SALARY.RANGE = 'F.GIC.SALARY.RANGE'
	F.GIC.SALARY.RANGE = ''
	CALL OPF(FN.GIC.SALARY.RANGE,F.GIC.SALARY.RANGE)
	
	Y.APPLICATION = 'EM.LO.APPLICATION':FM:'SECTOR':FM:'CUSTOMER'
    Y.FLD = 'AMR.GRC.PERIOD':VM:'L.AMR.INS.REQ':VM:'AMR.OCCUPATION':VM:'AMR.INS.CMPY':FM:'L.AMR.SEGMENT':VM:'L.AMR.INDUSTRY':VM:'L.AMR.SAL.RANGE':FM:'SEGMENT':VM:'SALARY.RANGE':VM:'EMPLOY.NAME' ;*INC0017410
    Y.POS = ''
    CALL MULTI.GET.LOC.REF(Y.APPLICATION,Y.FLD,Y.POS)
	
    Y.GRC.POS = Y.POS<1,1>
    Y.INS.REQ.POS = Y.POS<1,2>
    Y.AMR.OCCUPATION = Y.POS<1,3>
    Y.INS.CMPY.POS = Y.POS<1,4> ;*INC0017410
	Y.L.AMR.SEGMENT.POS = Y.POS<2,1>
	Y.L.AMR.INDUSTRY.POS = Y.POS<2,2>
	AMR.SAL.RANGE.POS = Y.POS<2,3>
	Y.SEGMENT.POS = Y.POS<3,1>
	Y.SALARY.RANGE.POS = Y.POS<3,2>
	AMR.CUS.EMP.NAME.POS = Y.POS<3,3>
	
	
***************    
RETURN
***************
 
***************
PROCESS:
***************
	
    Y.CUSTOMER = R.NEW(ELOA.CUSTOMER.ID)
    Y.INS.REQ = R.NEW(ELOA.LOCAL.REF)<1,Y.INS.REQ.POS>
    Y.INS.CMPY = R.NEW(ELOA.LOCAL.REF)<1,Y.INS.CMPY.POS> ;*INC0017410
    
    CALL F.READ(FN.CUSTOMER,Y.CUSTOMER,R.CUSTOMER,F.CUSTOMER,CUS.ERR)
    
    Y.DOB = R.CUSTOMER<EB.CUS.BIRTH.INCORP.DATE>
    Y.GENDER = R.CUSTOMER<EB.CUS.GENDER>
    Y.INDUSTRY = R.CUSTOMER<EB.CUS.INDUSTRY>
    Y.APPROVED.AMT = R.NEW(ELOA.APPR.AMOUNT)
    Y.LOAN.TERM = R.NEW(ELOA.APPR.TERM)
    Y.GRACE.PERIOD = R.NEW(ELOA.LOCAL.REF)<1,Y.GRC.POS>
    Y.LOAN.ID = ID.NEW
    Y.AMR.OCC.VAL = R.NEW(ELOA.LOCAL.REF)<1,Y.AMR.OCCUPATION> 
    Y.INT.RATE = R.NEW(ELOA.INT.RATE)
    GOSUB OFS.MSG

***************
RETURN
***************

***************
OFS.MSG:
***************

    IF Y.INS.REQ EQ 'YES' AND Y.INS.CMPY EQ 'AIA' THEN ;*INC0017410
        Y.RECORD<AIA.PRM.DATE.OF.BIRTH> = Y.DOB
        Y.RECORD<AIA.PRM.GENDER> = Y.GENDER
        Y.RECORD<AIA.PRM.INDUSTRY> = Y.INDUSTRY
        Y.RECORD<AIA.PRM.APPROVED.AMOUNT> = Y.APPROVED.AMT
        Y.RECORD<AIA.PRM.GRACE.PERIOD> = Y.GRACE.PERIOD
        Y.RECORD<AIA.PRM.LOAN.TERM> = Y.LOAN.TERM
        Y.RECORD<AIA.PRM.LOAN.APPLICATION.ID> = Y.LOAN.ID
        Y.RECORD<AIA.PRM.OCCUPATION> = Y.AMR.OCC.VAL	
        Y.RECORD<AIA.PRM.LOAN.INTEREST.RATE> = Y.INT.RATE

        Y.APP.NAME = 'AMR.AIA.LO.PRM.AMT'
        Y.OFSFUNCT = 'I'
        Y.PROCESS = 'PROCESS'
        Y.OFSVERSION = 'AMR.AIA.LO.PRM.AMT,AMR.AIA.PRM.AMT'
        Y.GTSMODE = ''
        Y.NO.OF.AUTH = 0
        Y.TRANSACTION.ID = ID.NEW
		AMR.ERROR =''
        OFS.SOURCE.ID = 'AMR'

        CALL OFS.BUILD.RECORD(Y.APP.NAME,Y.OFSFUNCT,Y.PROCESS,Y.OFSVERSION,Y.GTSMODE,Y.NO.OF.AUTH,Y.TRANSACTION.ID,Y.RECORD,Y.OFSRECORD)
		CALL OFS.POST.MESSAGE(Y.OFSRECORD,Y.TRANSACTION.ID,OFS.SOURCE.ID,AMR.ERROR)		
		
    END
	
***************
RETURN
***************
*Start-CHG0033351
***************
CHECKLOCN:
***************

	VAR.CUST.TYPE = R.NEW(ELOA.CUST.TYPE)
	IF VAR.CUST.TYPE EQ "INDIVIDUAL" THEN 
		Y.CUSTOMER =  R.NEW(ELOA.CUSTOMER.ID)
		VAR.OCCUPATION = R.NEW(ELOA.LOCAL.REF)<1,Y.AMR.OCCUPATION>
		GOSUB GETINCOME
	END ELSE
		VAR.GROUP.ID =  R.NEW(ELOA.GROUP.ID)
		MCB.GROUP.REC = ''
		MCB.GROUP.ERR = ''
		CALL F.READ(FN.MCB.GROUP,VAR.GROUP.ID,MCB.GROUP.REC,F.MCB.GROUP,MCB.GROUP.ERR)
		VAR.CUS.ID.LIST = R.NEW(ELOA.G.CUST.ID)
		VAR.G.CUS.LIST = MCB.GROUP.REC<MCB.G.M.NUMBER>
		CHANGE @VM TO @FM IN VAR.CUS.ID.LIST
		CHANGE @VM TO @FM IN VAR.G.CUS.LIST
		FOR I = 1 TO DCOUNT(VAR.CUS.ID.LIST,@FM)
			Y.CUSTOMER = FIELD(VAR.CUS.ID.LIST,@FM,I) ;*VAR.CUS.ID.LIST<I>
			LOCATE Y.CUSTOMER IN VAR.G.CUS.LIST SETTING VAR.POS THEN
				VAR.OCCUPATION =  MCB.GROUP.REC<MCB.G.M.NOTES,VAR.POS>
			END	
			GOSUB GETINCOME
		NEXT I	
	END
	
***************
RETURN
***************
***************
GETINCOME:
***************

	READ REC.CON FROM F.EM.INC.EXP, Y.CUSTOMER THEN
		VAR.INCOME.ASSET.CCY =  REC.CON<EM.INC.EXP.HOUSE.INC.CCY,3>
		VAR.AMT.MONTH.BORROW =  REC.CON<EM.INC.EXP.HOUSE.INC.AMT,3>
		IF VAR.INCOME.ASSET.CCY NE 'USD' THEN
			VAR.CHG.EXCH.CCY = 'USD'
			GOSUB PROCESS.EXCH.CCY
			VAR.AMT.MONTH.BORROW = DROUND((VAR.AMT.MONTH.BORROW*Y.SELL.AMT),Y.DECIMALS)
		END
		SECTOR.REC = ''
		SECTOR.ERR = ''
		CALL F.READ(FN.SECTOR,VAR.OCCUPATION,SECTOR.REC,F.SECTOR,SECTOR.ERR)
		VAR.SALARY.RANGE = SECTOR.REC<EB.SEC.LOCAL.REF,AMR.SAL.RANGE.POS>
		VAR.SEGMENT.LIST = SECTOR.REC<EB.SEC.LOCAL.REF,Y.L.AMR.SEGMENT.POS>
		VAR.INDUSTRY = SECTOR.REC<EB.SEC.LOCAL.REF,Y.L.AMR.INDUSTRY.POS>
		SALARY.RANGE.CNT =  DCOUNT(VAR.SALARY.RANGE,@SM)
		VAR.SEGMENT = ''
		FOR J = 1 TO SALARY.RANGE.CNT 
			VAR.SALARY.ID = VAR.SALARY.RANGE<1,1,J>
			GIC.SALARY.RANGE.REC = ''
			GIC.SALARY.RANGE.ERR = ''
			CALL F.READ(FN.GIC.SALARY.RANGE,VAR.SALARY.ID,GIC.SALARY.RANGE.REC,F.GIC.SALARY.RANGE,GIC.SALARY.RANGE.ERR)
			VAR.AMT.FROM = GIC.SALARY.RANGE.REC<GIC.SALARY.AMT.FROM>
			VAR.AMT.TO = GIC.SALARY.RANGE.REC<GIC.SALARY.RANGE.AMT.TO>
			IF VAR.AMT.MONTH.BORROW GE VAR.AMT.FROM AND VAR.AMT.MONTH.BORROW LE VAR.AMT.TO THEN 
				VAR.SEGMENT = VAR.SEGMENT.LIST<1,1,J>
				BREAK
			END	
		NEXT J
		IF VAR.SEGMENT NE '' THEN
			GOSUB OFS.MSG1
		END
	END
***************
RETURN
***************

***************
OFS.MSG1:
***************

	Y.RECORD = ''
	Y.SHORT.NAME.GB = R.CUSTOMER<EB.CUS.SHORT.NAME,1>
	Y.NAME.GB = R.CUSTOMER<EB.CUS.NAME.1,1>
	IF VAR.OCCUPATION GE 1001 AND VAR.OCCUPATION LE 1011 OR VAR.OCCUPATION GE 1014 AND VAR.OCCUPATION LE 1036 THEN
		Y.EMP.NAME = Y.SHORT.NAME.GB:" ":Y.NAME.GB
		Y.RECORD<EB.CUS.LOCAL.REF,AMR.CUS.EMP.NAME.POS> = Y.EMP.NAME	
	END	
	IF VAR.OCCUPATION EQ 1012 OR VAR.OCCUPATION EQ 1013 THEN
		Y.RECORD<EB.CUS.LOCAL.REF,AMR.CUS.EMP.NAME.POS> = "None"
	END
	
	Y.RECORD<EB.CUS.SECTOR> = VAR.OCCUPATION
	Y.RECORD<EB.CUS.LOCAL.REF,Y.SALARY.RANGE.POS> = VAR.SALARY.ID
	Y.RECORD<EB.CUS.LOCAL.REF,Y.SEGMENT.POS> = VAR.SEGMENT
    Y.RECORD<EB.CUS.INDUSTRY> = VAR.INDUSTRY

    Y.APP.NAME = 'CUSTOMER'
    Y.OFSFUNCT = 'I'
    Y.PROCESS = 'PROCESS'
    Y.OFSVERSION = 'CUSTOMER,DMI'
    Y.GTSMODE = ''
    Y.NO.OF.AUTH = 0
    Y.TRANSACTION.ID = Y.CUSTOMER
	AMR.ERROR =''
    OFS.SOURCE.ID = 'AMR'
	Y.OFSRECORD = ''

    CALL OFS.BUILD.RECORD(Y.APP.NAME,Y.OFSFUNCT,Y.PROCESS,Y.OFSVERSION,Y.GTSMODE,Y.NO.OF.AUTH,Y.TRANSACTION.ID,Y.RECORD,Y.OFSRECORD)
	CALL OFS.POST.MESSAGE(Y.OFSRECORD,Y.TRANSACTION.ID,OFS.SOURCE.ID,AMR.ERROR)	
		
***************
RETURN
***************

*------------------------
PROCESS.EXCH.CCY:
*------------------------

    Y.SELL.AMT = ''
    Y.BASE.CCY = ''
    Y.EXCH.RATE = ''
    Y.DIFF = ''
    Y.AMT.LCY = ''
    Y.RET.CODE = ''

    CALL EXCHRATE('1',VAR.INCOME.ASSET.CCY,'1',VAR.CHG.EXCH.CCY,Y.SELL.AMT,Y.BASE.CCY,Y.EXCH.RATE,Y.DIFF,Y.AMT.LCY,Y.RET.CODE)
    Y.DECIMALS = 0 ;* to calculate amount from 1.5 to 1 coz salary range no decimal
    IF VAR.CHG.EXCH.CCY EQ LCCY THEN
        Y.DECIMALS = 0
    END
    
    BEGIN CASE
        
        CASE VAR.INCOME.ASSET.CCY EQ 'KHR' AND VAR.CHG.EXCH.CCY EQ 'USD'
            Y.SELL.AMT = 1/4000
    
        CASE VAR.INCOME.ASSET.CCY EQ 'THB' AND VAR.CHG.EXCH.CCY EQ 'USD'
            Y.SELL.AMT = 1/40

        CASE VAR.INCOME.ASSET.CCY EQ VAR.CHG.EXCH.CCY
            Y.SELL.AMT = 1
			
    END CASE
	
*------------------------
RETURN
*------------------------
*End-CHG0033351
END
